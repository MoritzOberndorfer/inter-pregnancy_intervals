/*

"Do short inter-pregnancy intervals have an effect on gestational age? A register-based study of 1,024,004 pairs for first and second births from Finland, Austria and Norway"

This code file contains the data preparation, data analysis for Austrian, Finnish, and Norwegian data, and combination of results to produce our figures.

code author: Moritz Oberndorfer

*/


*set scheme s2mono
*SET UP FOLDER STRUCTURE
clear all
global username "`c(username)'"
di "$username"
*B14 Moritz
if "$username"== "$username" {
	global master ="C:\Users\moritzob\OneDrive - University of Helsinki\postdoc life helsinki\Interpregnancy interval letter"
	global code ="$master/scripts"
	global log ="$master\out\log"
	global dta ="$master\dta"
	global docx ="$master\out\docx"
	global graph ="$master\out\graphs"
	global table ="$master\out\tables"	
}
**# Bookmark #1
/*
FInnish data preparation
*/


/*
Study population:
Follow all women in Finland from their first singleton delivery at gestational week 22 or beyond during 1987 - 2009 until a consecutive singleton delivery before 2020
Also removed all pregnancies with less than 22 weeks
Also excluded women with one or two in-vitro pregnancies

Strategy:
Take all births until end of 2019
order them by mother id
keep only important variables: maternal age, gestation in week and days, date of conception, 
drop if birth order is greater than 2
drop if not singleton birth
drop if no second child

*/

*Base file containing the births 
use "D:\b14\external\thl_03_2023\synre.dta" , clear

graph set window fontface "Arial Narrow"

/* Data preparation */

destring tilastovuosi, generate(vuosi) force
rename aiti_shnro shnro
sort shnro vuosi

tab vuosi

drop if vuosi>2019

sort shnro vuosi
order shnro lapsi_shnro vuosi
gen bdate_child = dofc(LAPSEN_SYNTYMAPVM)
format bdate_child %td
drop LAPSEN_SYNTYMAPVM

*Length of gestational period into numeric format:
split kestovkpv , p("+")
destring kestovkpv1 kestovkpv2, replace force
				
*Length of gestational period in days:
gen gestationalage_days = kestovkpv1*7 + kestovkpv2
rename kestovkpv1 gestation_weeks
rename kestovkpv2 gestation_weeks_d
rename kestovkpv gestationalage
*Generating approxited day of conception:
gen conception_date = bdate_child-gestationalage_days
format conception_date %td

*drop if conceived before 2015
*drop if conception_date<td(01jan2015)

rename AITI_IKA mom_age
label variable mom_age "mother's age"

gen mom_age_cat = .
replace mom_age_cat = 5 if mom_age>=35
replace mom_age_cat = 4 if mom_age<35
replace mom_age_cat = 3 if mom_age<30
replace mom_age_cat = 2 if mom_age<25
replace mom_age_cat = 1 if mom_age<20

*Label mother's age.
label define aiti_ika 1 "<20" 2 "20-24" 3 "25-29" 4 "30-34" 5 "35+" 
label values mom_age_cat aiti_ika


encode sukup, gen(sex)
replace sex = 0 if sex == 2 
label define sukupuoli 0 girl 1 boy
label values sex sukupuoli
label variable sex "sex of the baby"


*mother's parity: convert into categorical variable, previous births over 4 as the ultimate class.
gen parity = aiemmatsynnytykset 
replace parity = 4 if parity>4
label define parit 0 "0" 1 "1" 2 "2" 3 "3" 4 "4+"
label values parity parit

*smoking information, whether smoked during pregnancy
encode tupakointitunnus, generate(smokinginformation)
*drop tupakointitunnus

replace smokinginformation = 0 if smokinginformation == 1 
replace smokinginformation = 1 if smokinginformation == 2 | smokinginformation == 3 | smokinginformation == 4 
replace smokinginformation = 2 if smokinginformation == 5 // remember to doublecheck whether this is correct. 

label define smoke  0 "no" 1 "yes" 2 "unknown"
label values smokinginformation smoke

*translate :)
rename (aiemmatraskaudet syntymapaino MONISIKI_SYNNYTYSTUNNUS) (priorpregnancies  birthweight  multiplefetus_id)
drop aiemmatsynnytykset tupakointitunnus sukup

sort shnro vuosi

*low birth weight <2500gr (binary)
gen byte lowweight = 0	if birthweight!=.
replace lowweight =	1 if birthweight<2500 & birthweight!=.

*preterm <37 week (binary) 
gen byte preterm = 0 if gestationalage_days!=.
replace preterm = 1 if 	gestationalage_days <259 & gestationalage_days!=.
replace preterm=. if gestationalage_days==.

gen cyear = year(conception_date)
gen cmonth = month(conception_date)

gen bmonth = month(bdate_child)
gen modate_b = ym(vuosi, bmonth)
gen bweek=week(bdate_child)
gen modate = ym(cyear, cmonth)
format modate %tm

*singletons
*tab multiplefetus_id 
destring multiplefetus_id, gen(multiplefetus_id2)

gen singleton=1
replace singleton=0 if multiplefetus_id2!=.

*number of singleton births for each mother
drop if singleton ==0 // exclude pregnancies with multiple babies
br shnro gestationalage gestation_weeks gestationalage_days vuosi
drop if gestation_weeks<22 // exclude if pregnancy lasted less than 22 weeks
drop if gestationalage_days ==. // exclude if gestational age is missing

tab ivf,m // only for 2017 and onwards
tab alkionsiirto,m // for 1996 and onwards
drop if ivf=="1"
drop if alkionsiirto=="1"

tab vuosi
sort shnro bdate_child
bysort shnro: gen total_singleton= _N 
bysort shnro: gen nr_singleton= _n 

*first birth needs to happen before 2010
gen a= 1 if vuosi<2010 & nr_singleton==1
bysort shnro: egen first_before_2010=max(a)
drop a
drop if first_before_2010!=1 // exclude if first born was after 2009
drop if total_singleton==1 // exclude those with no second child
tab total_singleton if nr_singleton==1
drop if nr_singleton>2 // only need first and second child

drop if shnro=="" // 2 with missing id
drop if lapsi_shnro=="" // something wrong with these 4,207 with missing child id. need to be excluded, otherwise some births are in there with negative interpregnany interval

*calculate interpregnancy interval in months
* date of second conception - date of previous birth 
sort shnro vuosi 
egen id=group(shnro)
xtset id nr_singleton
gen interpreg_days=conception_date - l1.bdate_child
gen interpreg_weeks=trunc(interpreg_days/7) // needs to be used as completed weeks to have integers
gen interpreg_months=trunc(interpreg_days/30.5) // needs to be used as completed months to have integers

*first outcome is difference in gestational length in days
gen diff_gestation_days=gestationalage_days-l1.gestationalage_days

tab interpreg_months,m // only 26 have 0 completed months
tab interpreg_days if interpreg_months==0 // most of them are very close to  complete 1 month 

**# Bookmark #2

/*
OUTPUT 1A
Difference between 2nd and 1st birth in days by inter-pregnancy interval in months 0-35

*/


frame create output1a mean_diff std_err ul95 ll95 pval observations

forval i=0/35{
mean diff_gestation_days if interpreg_months==`i'
scalar mean_diff=r(table)[1,1]
scalar std_err=r(table)[2,1]
scalar ul95=r(table)[6,1]
scalar ll95=r(table)[5,1]
scalar pval=r(table)[4,1]
scalar observations=r(table)[7,1]+1
frame post output1a (mean_diff) (std_err) (ul95) (ll95) (pval) (observations)

}

frame change output1a
gen month=_n-1

tw ///
(connected mean_diff month, lcolor(black)) ///
(rcap ul95 ll95 month, ) if month>0
*all here that needed for output1

export delimited using "$table\output1a.csv", replace


/*
OUTPUT 1B
Difference between 2nd and 1st birth in days by inter-pregnancy interval in months grouped by Eskild et al. groups

*/
frame change default

egen month_grp=cut(interpreg_months), at(0,6,12,18, 24,30 ,100) label
tab month_grp,m


frame create output1b  mean_diff std_err ul95 ll95 pval observations

forval i=0/5{
mean diff_gest if month_grp==`i' 
scalar mean_diff=r(table)[1,1]
scalar std_err=r(table)[2,1]
scalar ul95=r(table)[6,1]
scalar ll95=r(table)[5,1]
scalar pval=r(table)[4,1]
scalar observations=r(table)[7,1]+1
frame post output1b (mean_diff) (std_err) (ul95) (ll95) (pval) (observations)

}

frame change output1b

gen month=_n-1

tw ///
(connected mean_diff month, lcolor(black)) ///
(rcap ul95 ll95 month, ) 
*all here that needed for output1

export delimited using "$table\output1b.csv", replace

**# Bookmark #3

/*
OUTPUT 2A

Model gest 2= gest 1 + ib1. interpreg in months 0-35

*/

frame change default
reg gestationalage_days l1.gestationalage_days ib1.interpreg_months if interpreg_months<36 & nr_singleton==2
coefplot, ///
keep(*.interpreg_months) vertical ///
xlab(0(2)35, ) yline(0)
*look good

*now export the model estimates
reg gestationalage_days l1.gestationalage_days ib1.interpreg_months if interpreg_months<36 & nr_singleton==2
est store m1

estout m1 using "$table/output2a.csv", cells("b() se() ci_l ci_u p t") stats(N r2) replace



**# Bookmark #4

/*
OUTPUT 2B
Model gest 2= gest 1 + ib1. interpreg in months Eskild et al. groups

*/

tab month_grp, nolab

reg gestationalage_days l1.gestationalage_days ib1.interpreg_months if interpreg_months<6 & nr_singleton==2
est store m1

estout m1 using "$table/output2b.csv", cells("b() se() ci_l ci_u p t") stats(N r2) replace



**# Bookmark #5

/*

AUSTRIAN DATA


*/


clear all
import sas using "$dta\FreqSSW23oct24.sas7bdat",clear

drop if interpregMon > 35
rename interpregMon interpreg_months // to make other code work

gen diff_gest=schwdau_voll2 - schwdau_voll1

mean diff_gest if interpreg_months==1 [fweight=Count]
bysort interpreg_months: egen sum_obs=sum(Count)

/*
Addon: correlation coefficient for discussion
*/

corr schwdau_voll1 schwdau_voll2 interpreg_months [fweight=Count]
pwcorr schwdau_voll1 schwdau_voll2 interpreg_months [fweight=Count], star(0.05)


**# Bookmark #4

/*OUTPUT 1 A
get mean sd ci of difference between gestation 2nd -1dt birth, no model
*/


* post them from here into new frame. save the frame and export it.
frame create output1a month mean_diff std_err ul95 ll95 pval observations

forval i=0/35{
mean diff_gest if interpreg_months==`i' [fweight=Count]
scalar mean_diff=r(table)[1,1]
scalar std_err=r(table)[2,1]
scalar ul95=r(table)[6,1]
scalar ll95=r(table)[5,1]
scalar pval=r(table)[4,1]
scalar observations=r(table)[7,1]+1
scalar month=`i'
frame post output1a (month) (mean_diff) (std_err) (ul95) (ll95) (pval) (observations)

}

frame change output1a

tw ///
(connected mean_diff month, lcolor(black)) ///
(rcap ul95 ll95 month, ) if month>0
*all here that needed for output1

export delimited using "$table\AUT_output1a.csv", replace


**# Bookmark #1

/*
Output 1B
Recreate the Eskild groups
need to combine scwdau_voll1 schwdau_voll2 combination within each interval of inter-pregnancy interval

*/
frame change default
frame copy default eskild_grp
frame change eskild_grp
egen month_grp=cut(interpreg_months), at(0,6,12,18, 24,30 ,100) label
tab month_grp,m
egen grp1=group(schwdau_voll1 schwdau_voll2 month_grp ), label
sort grp1
bysort grp1: egen sum_grp1=sum(Count)

*For the eskild et al. groups we need to reduce data to aggregates

keep schwdau_voll1 schwdau_voll2 month_grp grp1 sum_grp1 diff_gest
duplicates drop grp1, force


*create results
frame create output1b  mean_diff std_err ul95 ll95 pval observations

forval i=0/5{
mean diff_gest if month_grp==`i' [fweight=sum_grp1]
scalar mean_diff=r(table)[1,1]
scalar std_err=r(table)[2,1]
scalar ul95=r(table)[6,1]
scalar ll95=r(table)[5,1]
scalar pval=r(table)[4,1]
scalar observations=r(table)[7,1]+1
frame post output1b (mean_diff) (std_err) (ul95) (ll95) (pval) (observations)

}

frame change output1b

gen month=_n-1

tw ///
(connected mean_diff month, lcolor(black)) ///
(rcap ul95 ll95 month, ) 
*all here that needed for output1

export delimited using "$table\AUT_output1b.csv", replace


**# Bookmark #6

/*
2 C Output
Difference with model. 
Gestational age in completed weeks due to data availability
Inter-pregnancy interval in Eskild groups

*/

frame change eskild_grp

*convert to days for comparability

foreach var of varlist schwdau_voll1 schwdau_voll2 {
	replace `var' = `var' *7
	
}


reg schwdau_voll2 schwdau_voll1 i.month_grp [fweight=sum_grp1]
coefplot, ///
keep(*.month_grp) vertical ///
xlab(0(1)5, ) yline(0)


*now export the model estimates
reg schwdau_voll2 schwdau_voll1 i.month_grp [fweight=sum_grp1]
est store m1

estout m1 using "$table/AUT_output2c.csv", cells("b() se() ci_l ci_u p t") stats(N r2) replace

/*
OUTPUt 2D , 
Difference with model. 
Gestational age in completed weeks due to data availability
Inter-pregnancy interval 0-35

*/


frame change default
count
*look good

*convert to days
foreach var of varlist schwdau_voll1 schwdau_voll2 {
	replace `var' = `var' *7
	
}

reg schwdau_voll2 schwdau_voll1 ib1.interpreg_months [fweight=Count]
coefplot, ///
keep(*.interpreg_months) vertical ///
xlab(0(2)35, ) yline(0)


*now export the model estimates
reg schwdau_voll2 schwdau_voll1 ib1.interpreg_months [fweight=Count]
est store m1

estout m1 using "$table/AUT_output2d.csv", cells("b() se() ci_l ci_u p t") stats(N r2) replace

**# Bookmark #1

/*
-------------------------------------------------------------
-------------------------------------------------------------

NORWEGIAN DATA

-------------------------------------------------------------
-------------------------------------------------------------


*/

/*
This code assumes the data is prepared as in Eskild et al. 2024

Exclusion/Inclusion criteria: 
.) Inclde Singleton births only
.) Include 22+0 weeks or higher gestational age only (<22 weeks gestational age excluded)
.) Exclude women with one or two pregnancies after IVF
.) Included if first birth before 2010
.) Second birth before 2020
.) Non-missing information on gestational age.

#####################################
Data needs to be in long format.
Pregnancies nested within Women
######################################

We only look at pair of births with inter-pregnancy intervals smaller than 36 months

#########
Variables necessary for the code:

Personal ID of woman: ID

Variable indicating first or second singleton pregnancy: nr_singleton
nr_singleton=1 for first born
nr_singleton=2 for second born


inter-pregnancy interval in months: interpreg_month
range of interpreg_month: 0-35

gestational age in days for births: gestationalage_days

#########

This code will create 4 tables from which we will create 4 figures.
They correspond to the country-specific panels in the 2 figures sent. 

Output 1: 
Means differences in gestational age (second - first born) and their 95% confidence intervals by pregnancy interval with the groups of inter-pregnancy intervals used in Eskild et al. 

Output 2:
Means differences in gestational age (second - first born) and their 95% confidence intervals by pregnancy interval from 1 completed month to 35 completed months.

Output 3: 
Effects of inter-pregnancy intervals (in Eskild et al. groups) on gestational age of second born estimated by linear regression models adjusting for gestational age of first born. The output shows the estimated coefficients and 95% confidence intervals for each inter-pregnancy interval with 0-5 completed months being the reference group.

Output 4: 
Effects of inter-pregnancy intervals in completed months on gestational age of second born estimated by linear regression models adjusting for gestational age of first born. The output shows the estimated coefficients and 95% confidence intervals for each inter-pregnancy interval with 1 completed month being the reference group.


*/


**# Bookmark #1

*#########################################################################
*###############  FILL IN VARIABLE NAMES BELOW ###########################
*#########################################################################

global table "FILE PATH HERE" // insert path to folder where output will be saved

*First, specify the variable names, so that the code runs

global ID "XXXX" // insert variable name for women IDs instead of XXXX

global nr_singleton "XXXX" // insert variable name for order of first (=1) and second (=2) singleton birth

global interpreg_month "XXXX" // insert variabe name for your variable counting the inter-pregnancy interval between first birth and conception of second child in completed months 

global gestationalage_days "XXXX" // insert variable name for your variable for gestational age in days for each birth

*#########################################################################
*#########################################################################
*#########################################################################

**# Bookmark #7
 

*Generate the output necessary to produce the figures of interest.

xtset $ID $nr_singleton // needed to use lag operators

gen diff_gestation_days=$gestationalage_days - l1.$gestationalage_days // calculates the difference in gestational age in days between first and second birth. This variable will only have a value for the second pregnancy of each women.


egen month_grp=cut($interpreg_months), at(0,6,12,18, 24,30 ,36, 42,48,54,60,66,72,78,84,90,96,102,108,124,10000) label
tab month_grp,m


**# Bookmark #5

/*
OUTPUT 1
Difference between 2nd and 1st birth in days by inter-pregnancy interval in months grouped by Eskild et al. groups

*/


frame create output1 mean_diff std_err ul95 ll95 pval observations

forval i=0/5{
mean $diff_gestation_days if month_grp==`i' 
scalar mean_diff=r(table)[1,1]
scalar std_err=r(table)[2,1]
scalar ul95=r(table)[6,1]
scalar ll95=r(table)[5,1]
scalar pval=r(table)[4,1]
scalar observations=r(table)[7,1]+1
frame post output1 (mean_diff) (std_err) (ul95) (ll95) (pval) (observations)

}

frame change output1

gen month=_n-1

tw ///
(connected mean_diff month, lcolor(black)) ///
(rcap ul95 ll95 month, ) 
*all here that needed for output1

export delimited using "$table\output1.csv", replace

**# Bookmark #3


/*
OUTPUT 2
Difference between 2nd and 1st birth in days by inter-pregnancy interval in months 0-35

*/
frame change default
frame create output2 mean_diff std_err ul95 ll95 pval observations

forval i=0/35{
mean $diff_gestation_days if $interpreg_months==`i'
scalar mean_diff=r(table)[1,1]
scalar std_err=r(table)[2,1]
scalar ul95=r(table)[6,1]
scalar ll95=r(table)[5,1]
scalar pval=r(table)[4,1]
scalar observations=r(table)[7,1]+1
frame post output2 (mean_diff) (std_err) (ul95) (ll95) (pval) (observations)

}

frame change output2
gen month=_n-1

tw ///
(connected mean_diff month, lcolor(black)) ///
(rcap ul95 ll95 month, ) if month>0
*all here that needed for output2

export delimited using "$table\output2.csv", replace

**# Bookmark #4

/*
OUTPUT 3
Model gest 2= gest 1 + ib1. interpreg in months Eskild et al. groups

*/

frame change default
tab month_grp, nolab

reg $gestationalage_days l1.$gestationalage_days i.month_grp if i.month_grp<6 & $nr_singleton ==2
est store m1

estout m1 using "$table/output3.csv", cells("b() se() ci_l ci_u p t") stats(N r2) replace


**# Bookmark #6

/*
OUTPUT 4

Model gest 2= gest 1 + ib1. interpreg in months 0-35

*/

reg $gestationalage_days l1.$gestationalage_days ib1.$interpreg_months if $interpreg_months <36 & $nr_singleton ==2

*now export the model estimates
est store m2

estout m2 using "$table/output4.csv", cells("b() se() ci_l ci_u p t") stats(N r2) replace


/* 

Please send the files output1, output2, output3, output4 to moritz.oberndorfer@helsinki.fi 
I will use these ouputs to create figures combining Austrian, Finnish, and Norwegian results.

Thank you so much! I hope the code runs smoothly.

Last line.
*/



**# Bookmark #2


/*
-------------------------------------------------------------
-------------------------------------------------------------

USE OUTPUTS TO CREATE FIGURE 1

-------------------------------------------------------------
-------------------------------------------------------------


*/

import delimited "$dta\output1a.csv", clear 
gen finland=1 // indicator for Finland with each month
save "$dta\output1a.dta",replace


import delimited "$dta\output1b.csv", clear 
gen finland=2 // indicator for Finland with each month with Eskild grps
save "$dta\output1b.dta",replace


import delimited "$table\AUT_output1a.csv", clear 
gen finland=0 // indicator for AUT with each month
save "$dta\AUT_output1a.dta",replace

import delimited "$table\AUT_output1b.csv", clear 
gen finland=-1 // indicator for AUT with Eskild groups
save "$dta\AUT_output1b.dta",replace

import delimited "$dta\NOR_output1b.csv", clear 
gen finland=3 // indicator for Norway eskild groups
save "$dta\NOR_output1.dta",replace

import delimited "$dta\NOR_output2b.csv", clear 
gen finland=4 // indicator for Norway each month
save "$dta\NOR_output2.dta",replace


append using "$dta\output1a.dta"
append using "$dta\output1b.dta"
append using "$dta\AUT_output1a.dta"
append using "$dta\AUT_output1b.dta"
append using "$dta\NOR_output1.dta"


*create figure 1

*PANEL A left top (Finland, in Eskild groups)
*correct x axis labels for eskild groups
replace month=month+1 if finland ==2
replace month=month+1 if finland ==-1
replace month=month+1 if finland ==3


tw ///
(scatter mean_diff month if finland==2, lcolor(black) mcolor(black) msize(tiny)) ///
(rspike ul95 ll95 month if finland==2,  lcolor(black) lwidth(thin))  ///
, ///
yt("Gestational Age Second Born - First Born in Days",  size(medsmall)) ///
xt("Inter-pregnancy Interval in Months") ///
xlab(0 " " 1 "0-5" 2 "6-11" 3 "12-17" 4 "18-23" 5 "24-29" 6 "30-35", noticks nogrid) ///
ylab(-1(0.5) 4, gmin) ///
yline(0, lcolor(red) lpattern(solid)) ///
leg(off) ///
title("{it:Panel A:} {bf:Finland}", size(medium)) ///
name(FIN1, replace)



tw ///
(scatter mean_diff month if finland==1, lcolor(black) mcolor(black) msize(tiny)) ///
(rspike ul95 ll95 month if finland==1, lcolor(black) lwidth(thin)) if month>0  ///
, ///
yt("Gestational Age 2nd Birth - 1st Birth in days", size(medsmall)) ///
xt("Inter-pregnancy Interval in Months") ///
xlab(1(2)35, noticks nogrid) ///
ylab(-1(2)15, gmin) ///
yline(0, lcolor(red) lpattern(solid)) ///
leg(off) ///
title("{it:Panel D:} {bf:Finland}", size(medium)) ///
name(FIN2, replace)

grc1leg2 FIN1 FIN2, col(1) xtob1title ytol1title   name(col1,replace)


*Convert Austrian effects to days by multiplying with 7

foreach var of varlist mean_diff ul95 ll95 {
	replace `var' = `var' *7 if finland==-1
	replace `var' = `var' *7 if finland==0
}

tw ///
(scatter mean_diff month if finland==-1, lcolor(black) mcolor(black) msize(tiny)) ///
(rspike ul95 ll95 month if finland==-1, lcolor(black) lwidth(thin)) ///
, ///
yt("",size(tiny) ) ///
xt("Inter-pregnancy Interval in Months") ///
xlab(0 " " 1 "0-5" 2 "6-11" 3 "12-17" 4 "18-23" 5 "24-29" 6 "30-35", noticks nogrid) ///
ylab(-1(0.5) 4, gmin) ///
yline(0, lcolor(red) lpattern(solid)) ///
leg(off) ///
title("{it:Panel B:} {bf:Austria}",size(medium)) ///
name(AT1, replace)


tw ///
(scatter mean_diff month if finland==0, lcolor(black) mcolor(black) msize(tiny)) ///
(rspike ul95 ll95 month if finland==0, lcolor(black) lwidth(thin)) if month>0 ///
, ///
yt("",size(tiny)) ///
xt("Inter-pregnancy Interval in Months") ///
xlab(1(2)35, noticks nogrid) ///
ylab(-1(2)15, gmin) ///
yline(0, lcolor(red) lpattern(solid)) ///
leg(off) ///
title("{it:Panel E:} {bf:Austria}",size(medium)) ///
name(AT2, replace)


grc1leg2 AT1 AT2, col(1) xtob1title ytol1title   name(col2,replace)




tw ///
(scatter mean_diff month if finland==3, lcolor(black) mcolor(black) msize(tiny)) ///
(rspike ul95 ll95 month if finland==3,  lcolor(black) lwidth(thin))  ///
, ///
yt("",  size(medsmall)) ///
xt("Inter-pregnancy Interval in Months") ///
xlab(0 " " 1 "0-5" 2 "6-11" 3 "12-17" 4 "18-23" 5 "24-29" 6 "30-35", noticks nogrid) ///
ylab(-1(0.5) 4, gmin) ///
yline(0, lcolor(red) lpattern(solid)) ///
leg(off) ///
title("{it:Panel C:} {bf:Norway}", size(medium)) ///
name(NOR1, replace)



tw ///
(scatter mean_diff month if finland==4, lcolor(black) mcolor(black) msize(tiny)) ///
(rspike ul95 ll95 month if finland==4, lcolor(black) lwidth(thin))  ///
, ///
yt("", size(medsmall)) ///
xt("Inter-pregnancy Interval in Months") ///
xlab(0(2)35, noticks nogrid) ///
ylab(-1(2)15, gmin) ///
yline(0, lcolor(red) lpattern(solid)) ///
leg(off) ///
title("{it:Panel F:} {bf:Norway}", size(medium)) ///
name(NOR2, replace)

grc1leg2 NOR1 NOR2, col(1) xtob1title ytol1title  name(col3,replace)

grc1leg2 col1 col2 col3, ///
col(3) loff imargins(zero) ycommon ///
note("Differences for inter-pregnancy intervals smaller than 1 month are omitted in the lower panels due to few observations (AT: n=26, FI: n=24)" "For Austria, we used completed weeks due to data availability. Means of differences in gestational age (no regression models)" , size(vsmall))

/*///
note("Differences for inter-pregnancy intervals smaller than 1 month are omitted in the lower panels due to few observations (AT: n=26, FI: n=24)" "For Austria, we used completed weeks due to data availability" "Means of differences in gestational age (no regression models)" , size(vsmall))*/


graph export "$graph/figure1_$S_DATE.svg",replace
graph export "$graph/figure1_$S_DATE.pdf",replace

*create table for appendix

drop std_err
gen austria=1 if finland==0
replace austria=2 if finland==-1

gen norway=1 if finland==3
replace norway=2 if finland==4

replace finland=. if finland==0 | finland==-1 | finland==3 | finland==4

gen month_grp= month if finland==2 | finland==-1 | finland==3
label define month_lab 1 "0-5" 2 "6-11" 3 "12-17" 4 "18-23" 5 "24-29" 6 "30-35"
label values month_grp month_lab 
order austria finland norway month month_grp
sort austria finland norway month
export excel using "$table\Figure1_data_$S_DATE.xls", firstrow(variables)  replace

**# Bookmark #4

/*
-------------------------------------------------------------
-------------------------------------------------------------

USE OUTPUTS TO CREATE FIGURE 2

-------------------------------------------------------------
-------------------------------------------------------------


*/


* OUTPUT 2B Finland results based on Eskild et al. groups
import delimited "$dta\output2b.csv", clear 

gen finland=1
drop if v2=="m1"

rename v1 coef
rename v2 b 
rename v3 se
rename v4 min95 
rename v5 max95
rename v6 pval 
rename v7 t

drop in 1

destring b se min95 max95 pval t, replace
drop in 1
drop in 9
drop in 8 
drop in 7


gen month=_n-1

save "$dta\output2b.dta",replace

*OUTPUT 2A Finland

import delimited "$dta\output2a.csv", clear 

gen finland=2
drop if v2=="m1"

rename v1 coef
rename v2 b 
rename v3 se
rename v4 min95 
rename v5 max95
rename v6 pval 
rename v7 t

drop in 1

destring b se min95 max95 pval t, replace
drop in 1
drop in 39 
drop in 38 
drop in 37

gen month=_n-1

save "$dta\output2a.dta",replace

*Austria output 2b

import delimited "$table\AUT_output2c.csv", clear 

gen finland=0
drop if v2=="m1"

rename v1 coef
rename v2 b 
rename v3 se
rename v4 min95 
rename v5 max95
rename v6 pval 
rename v7 t

drop in 1

destring b se min95 max95 pval t, replace
drop in 1
drop in 9
drop in 8 
drop in 7


gen month=_n-1

save "$dta\AUT_output2c.dta",replace

*OUTPUT 2A AUT

import delimited "$table\AUT_output2d.csv", clear 

gen finland=-1
drop if v2=="m1"

rename v1 coef
rename v2 b 
rename v3 se
rename v4 min95 
rename v5 max95
rename v6 pval 
rename v7 t

drop in 1

destring b se min95 max95 pval t, replace
drop in 1
drop in 39 
drop in 38 
drop in 37

gen month=_n-1

save "$dta\AUT_output2d.dta",replace

*NORWAY OUTPUT 3 Model with Eskild groups

import delimited "$dta\NOR_output3b.csv", clear 

gen finland=3
drop if v2=="m1"

rename v1 coef
rename v2 b 
rename v3 se
rename v4 min95 
rename v5 max95
rename v6 pval 
rename v7 t

drop in 1

destring b se min95 max95 pval t, replace
drop in 1
drop in 9
drop in 8 
drop in 7

gen month=_n-1

save "$dta\NOR_output3.dta",replace

*NORWAY OUTPUT 3 Model for each month

import delimited "$dta/NOR_output4b.csv", clear 

gen finland=4
drop if v2=="m2"

rename v1 coef
rename v2 b 
rename v3 se
rename v4 min95 
rename v5 max95
rename v6 pval 
rename v7 t

drop in 1

destring b se min95 max95 pval t, replace
drop in 1
drop in 39 
drop in 38 
drop in 37

gen month=_n-1

save "$dta\NOR_output4.dta",replace


**# Bookmark #12
*Merge data to create figure 2
use "$dta\NOR_output4.dta",clear
append using "$dta\NOR_output3.dta"
append using "$dta\output2a.dta"
append using "$dta\output2b.dta"
append using "$dta\AUT_output2c.dta"
append using "$dta\AUT_output2d.dta"

sort finland month
replace month=month+1 if finland ==1
replace month=month+1 if finland ==0
replace month=month+1 if finland ==3

*preserve 
*drop if month==0 // messing with the axis

tw ///
(scatter b month if finland==1 & month>0, lcolor(black) mcolor(black) msize(tiny)) ///
(rspike max95 min95 month if finland==1 & month>0, lcolor(black) lwidth(thin)) ///
, ///
yt("Effect of Inter-pregnancy Interval on Gestational Age (days) of Second Born", size(medsmall)) ///
xt("Inter-pregnancy Interval in Months") ///
xlab(0 " " 1 "0-5" 2 "6-11" 3 "12-17" 4 "18-23" 5 "24-29" 6 "30-35", noticks nogrid) ///
ylab(-1(0.5)2.5, gmin ) ///
yline(0, lcolor(red) lpattern(solid)) ///
leg(off) ///
title("{it:Panel A:} {bf:Finland}", size(medium)) ///
name(FIN1, replace)


tw ///
(scatter b month if finland==2 & month>0 , lcolor(black) mcolor(black) msize(tiny)) ///
(rspike max95 min95 month if finland==2 & month>0, lcolor(black) lwidth(thin)) ///
, ///
yt("Effect on Gestational Age (days) of 2nd Birth", size(medsmall)) ///
xt("Inter-pregnancy Interval in Months") ///
xlab(1(2)35, nogrid noticks) ///
ylab(-1.5(0.5)2.5, gmin ) ///
yline(0, lcolor(red) lpattern(solid)) ///
leg(off) ///
title("{it:Panel D:} {bf:Finland}", size(medium)) ///
name(FIN2, replace)

grc1leg2 FIN1 FIN2, col(1) xtob1title ytol1title loff name(col1,replace)

*Austrian effects are completed weeks * 7

tw ///
(scatter b month if finland==0 & month>0, lcolor(black) mcolor(black) msize(tiny)) ///
(rspike max95 min95 month if finland==0 & month>0, lcolor(black) lwidth(thin)) ///
, ///
yt("", size(tiny)) ///
xt("Inter-pregnancy Interval in Months") ///
xlab(0 " " 1 "0-5" 2 "6-11" 3 "12-17" 4 "18-23" 5 "24-29" 6 "30-35", noticks nogrid) ///
ylab(-1(0.5)2.5, gmin ) ///
yline(0, lcolor(red) lpattern(solid)) ///
leg(off) ///
title("{it:Panel B:} {bf:Austria}", size(medium)) ///
name(AT1, replace)


tw ///
(scatter b month if finland==-1 & month>0 , lcolor(black) mcolor(black) msize(tiny)) ///
(rspike max95 min95 month if finland==-1 & month>0, lcolor(black) lwidth(thin)) ///
, ///
yt("", size(tiny)) ///
xt("Inter-pregnancy Interval in Months") ///
xlab(1(2)35, nogrid noticks) ///
ylab(-1.5(0.5)2.5, gmin ) ///
yline(0, lcolor(red) lpattern(solid)) ///
leg(off) ///
title("{it:Panel E:} {bf:Austria}", size(medium)) ///
name(AT2, replace)

grc1leg2 AT1 AT2, col(1) xtob1title ytol1title loff  name(col2,replace)


tw ///
(scatter b month if finland==3 & month>0, lcolor(black) mcolor(black) msize(tiny)) ///
(rspike max95 min95 month if finland==3 & month>0, lcolor(black) lwidth(thin)) ///
, ///
yt(" ", size(medsmall)) ///
xt("Inter-pregnancy Interval in Months") ///
xlab(0 " " 1 "0-5" 2 "6-11" 3 "12-17" 4 "18-23" 5 "24-29" 6 "30-35", noticks nogrid) ///
ylab(-1(0.5)2.5, gmin ) ///
yline(0, lcolor(red) lpattern(solid)) ///
leg(off) ///
title("{it:Panel C:} {bf:Norway}", size(medium)) ///
name(NOR1, replace)


tw ///
(scatter b month if finland==4  , lcolor(black) mcolor(black) msize(tiny)) ///
(rspike max95 min95 month if finland==4  , lcolor(black) lwidth(thin)) ///
, ///
yt("Effect on Gestational Age (days) of 2nd Birth", size(medsmall)) ///
xt("Inter-pregnancy Interval in Months") ///
xlab(0(2)35, nogrid noticks) ///
ylab(-1.5(0.5)2.5, gmin ) ///
yline(0, lcolor(red) lpattern(solid)) ///
leg(off) ///
title("{it:Panel F:} {bf:Norway}", size(medium)) ///
name(NOR2, replace)

grc1leg2 NOR1 NOR2, col(1) xtob1title ytol1title loff name(col3,replace)


**# Bookmark #5


*combining

grc1leg2 col1 col2 col3, ///
col(3) loff  imargins(zero)

/* ///
note("Estimates for inter-pregnancy intervals smaller than 1 month are omitted in the lower panels due to few observations (AT: n=26, FI: n=24)" "For Austria, we used completed weeks due to data availability." , size(vsmall)) */


graph export "$graph/figure2_$S_DATE.svg",replace
graph export "$graph/figure2_$S_DATE.pdf",replace


*create table for appendix
*Merge data to create figure 2
use "$dta\NOR_output4.dta",clear
append using "$dta\NOR_output3.dta"
append using "$dta\output2a.dta"
append using "$dta\output2b.dta"
append using "$dta\AUT_output2c.dta"
append using "$dta\AUT_output2d.dta"

sort finland month
replace month=month+1 if finland ==1
replace month=month+1 if finland ==0
replace month=month+1 if finland ==3

gen austria=1 if finland==0
replace austria=2 if finland==-1

gen norway=1 if finland==3
replace norway=2 if finland==4
replace finland=. if finland==0 | finland==-1 | finland==3 | finland==4


gen month_grp= month if austria==1 | finland==1 | norway==3
label define month_lab 1 "0-5" 2 "6-11" 3 "12-17" 4 "18-23" 5 "24-29" 6 "30-35"
label values month_grp month_lab 

order austria finland norway month month_grp
sort austria finland norway month
export excel using "$table\Figure2_data_$S_DATE.xls", firstrow(variables)  replace



